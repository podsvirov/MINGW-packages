# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_realname=osgQtQuick
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}" "${MINGW_PACKAGE_PREFIX}-${_realname}-debug")
_pkgver=v2.0.0-alpha-2
pkgver=2.0.0.7
pkgrel=1
pkgdesc="OpenSceneGraph QML Modules (mingw-w64)"
arch=('any')
license=('LGPL')
url="https://github.com/podsvirov/osgqtquick"
depends=("${MINGW_PACKAGE_PREFIX}-OpenSceneGraph"
         "${MINGW_PACKAGE_PREFIX}-qt5")
makedepends=("make"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-OpenSceneGraph-debug")
options=(!strip staticlibs !buildflags)
source=(https://github.com/podsvirov/osgqtquick/archive/v2.0.0-alpha-2.zip)
md5sums=('SKIP')

build() {

  for builddir in {Release,Debug}-${MINGW_CHOST}; do
    [[ -d ${builddir} ]] && rm -rf ${builddir}
    mkdir -p ${builddir}
    pushd ${builddir}
      [[ "${builddir%-${MINGW_CHOST}}" == "Release" ]] && {
        local _opts="-DosgQtQuick_BUILD_DOCS:BOOL=ON"
      } || {
        local _opts="-DosgQtQuick_BUILD_DOCS:BOOL=OFF"
      }

      msg "Building ${builddir%-${MINGW_CHOST}} version..."

      MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
      COLLADA_DIR=${MINGW_PREFIX} \
      ${MINGW_PREFIX}/bin/cmake.exe \
        -G"MSYS Makefiles" \
        -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
        -DCMAKE_BUILD_TYPE=${builddir%-${MINGW_CHOST}} \
        ${_opts} \
        ../osgqtquick-2.0.0-alpha-2

      make
    popd
  done
}

package_release() {
  cd Release-${MINGW_CHOST}
  make DESTDIR=${pkgdir} -j1 install
}

package_debug() {
  depends=("${MINGW_PACKAGE_PREFIX}-${_realname}=$pkgver")
  options=('staticlibs' '!strip')
  pkgdesc="OpenSceneGraph QML Modules (debug) (mingw-w64)"

  cd Debug-${MINGW_CHOST}
  make DESTDIR=${pkgdir} -j1 install

  rm -rf ${pkgdir}${MINGW_PREFIX}/include
  rm -rf ${pkgdir}${MINGW_PREFIX}/lib/cmake/osgQtQuick/osgQtQuickConfig.cmake
  rm -rf ${pkgdir}${MINGW_PREFIX}/lib/cmake/osgQtQuick/osgQtQuickConfigVersion.cmake
  rm -rf ${pkgdir}${MINGW_PREFIX}/lib/cmake/osgQtQuick/osgQtQuickTargets.cmake
  rm -rf ${pkgdir}${MINGW_PREFIX}/share/qt5/qml/*/qmldir
}

package_mingw-w64-i686-osgQtQuick() {
  package_release
}

package_mingw-w64-i686-osgQtQuick-debug() {
  package_debug
}

package_mingw-w64-x86_64-osgQtQuick() {
  package_release
}

package_mingw-w64-x86_64-osgQtQuick-debug() {
  package_debug
}
