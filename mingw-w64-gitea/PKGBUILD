# Maintainer: Konstantin Podsvirov <konstantin@podsvirov.pro>

_realname=gitea
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.19.1
pkgrel=1
pkgdesc='Painless self-hosted Git service, community managed (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://github.com/go-gitea/gitea"
license=('MIT')
depends=("git")
makedepends=("${MINGW_PACKAGE_PREFIX}-go"
             "${MINGW_PACKAGE_PREFIX}-nodejs"
             "${MINGW_PACKAGE_PREFIX}-cc")
optdepends=("${MINGW_PACKAGE_PREFIX}-mariadb: MariaDB support"
            "${MINGW_PACKAGE_PREFIX}-postgresql: PostgreSQL support"
            "${MINGW_PACKAGE_PREFIX}-sqlite3: SQLite support"
	    "openssh: GIT over SSH support")
source=("$_realname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('1ea9397527606b80f246461860995688e7bc114d129430688c2aac17d5f7ce6f')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  make deps
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  export GOOS=windows
  export GOROOT=${MINGW_PREFIX}/lib/go
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GO_LDFLAGS="-s -w"
  export GOFLAGS="-trimpath -mod=readonly -modcacherw -ldflags=-linkmode=external"

  make
}


package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  install -Dm755 "gitea.exe" "${pkgdir}${MINGW_PREFIX}/bin/"
  install -Dm644 "custom/conf/app.example.ini" "${pkgdir}${MINGW_PREFIX}/etc/${_realname}/app.example.ini"
  install -Dm644 "LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
